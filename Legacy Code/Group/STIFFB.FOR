C///////////////////////////////////////////////////////////////////////
C// BEToolbox - Bridge Engineering Toolbox
C// Copyright (C) 2000, Washington State Department of Transportation
C//                     Bridge and Structures Office
C//
C// This software was developed as part of the Alternate Route Project
C//
C// This program is free software; you can redistribute it and/or modify
C// it under the terms of the Alternate Route Open Source License as
C// published by the Washington State Department of Transportation,
C// Bridge and Structures Office.
C//
C// This program is distributed in the hope that it will be useful,
C// but is distributed AS IS, WITHOUT ANY WARRANTY; without even the
C// implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR
C// PURPOSE.  See the Alternate Route Open Source License for more details.
C//
C// You should have received a copy of the Alternate Open Source License
C// along with this program; if not, write to the Washington State
C// Department of Transportation, Bridge and Structures Office,
C// 4500 3rd Ave SE, P.O. Box 47340, Olympia, WA 98503, USA or e-mail
C// Bridge_Support@wsdot.wa.gov
C///////////////////////////////////////////////////////////////////////

      SUBROUTINE STIFFB(ID,L)
C
C     REVISION LOG:
C
C     RICHARD BRICE - RECOMPILED FOR VMS VERSION 5.5 - 04/27/93
C
C     THIS ROUTINE DOES THE FOLLOWING:
C     1)  COMPUTES VALUES OF SOIL MODULUS AT POINTS ON THE PILE THAT
C         ARE IN A LAYER SPECIFIED AS A STIFF CLAY BELOW THE WATER
C         TABLE
C     2)  GENERATES P-Y CURVES AT SPECIFIED DEPTHS USING THE STIFF CLAY
C         BELOW THE WATER TABLE CRITERIA (REESE, ET AL. 1975)
C
C
      IMPLICIT REAL*8 (A-H), REAL*8 (P-Z)
      REAL*8 LENGTH,K
      CHARACTER*8 TC
      CHARACTER*4 ISYSTM
      CHARACTER*8 UN1,UN2,UN3,UN4,UN5,UN6,UN7,UN8,UN9,UN10,UN11
C
      COMMON / BLOC00 / TC(20)
      COMMON / BLOC04 / POTT(20),
     1                  DISTB(20),DISTA(20),THETA(20),DPS(20),
     2                  FDBET(20),KNPL,KP(20),KA(20),KS(20)
      COMMON /BLOC01/ XN(20,307),DIAM,PI,EM14,EM6,EM5,LENGTH(20),NN
      COMMON /BLOC02/ XTOP(10),XBOT(10),XGS,NL,ITOP(10),IBOT(10)
      COMMON /BLOC03/ ES(307),Y(307),LAYER(10),K(10)
      COMMON /BLOC05/ TOLP,KSOIL(10),ITOPL,IBOTL,MAXIT
      COMMON /BLOC09/ YINC,XSP(10),RCYCL,KTHRU,KCYCL,KPYOP,IT,IGP
      COMMON /BLOC14/ UN1,UN2,UN3,UN4,UN5,UN6,UN7,UN8,UN9,UN10,UN11,
     1 ISYSTM
      COMMON /BLOC19/ IC
      IC=ID
C
C     ...BEGIN THE SOLUTION LOOP FOR A PARTICULAR DEPTH
      DO 230 I=ITOPL,IBOTL
C
C     ...STORE FUNCTION VALUES AND ARRAY VALUES TO LOCAL VARIABLES
      X=XN(IC,I)
      IF (KPYOP.NE.-1) GO TO 10
      DX=DIAM
      GO TO 20
   10 DX=D(IC,X)
   20 CX=C(X)
      CAX=CAVG(X)
      GAMX=GAM(X)
      E50X=E50(X)
      ASX=AS(X)
      ACX=AC(X)
C
C     ...COMPUTE DEPTH OF COORDINATE BELOW GROUND SURFACE
      IF (KPYOP.EQ.-1) GO TO 30
      IF (KPYOP.EQ.0) XGS=DPS(IT)
      IF (KPYOP.EQ.1) XGS=DPS(IGP)
   30 DEPTH=X-XGS
C
C     ...PRINT HEADING FOR GENERATED P-Y CURVES
      IF (KTHRU.EQ.0) GO TO 40
      WRITE (6,240) UN3,UN3,UN4,UN5,UN3,UN7,UN8,UN4,UN5,UN3,UN7,UN8,UN4,
     1UN5,UN3,UN7,UN9,DEPTH,DX,CX,CAX,GAMX,E50X
      WRITE (6,250) ASX,ACX,UN3,UN4,UN5,UN3
C
C     ...STORE ABSOLUTE VALUE OF DEFLECTION
   40 YA=DABS(Y(I))
C
C     ...COMPUTE MAXIMUM VALUE OF SOIL MODULUS
      ESLIM=K(L)*DEPTH
C
C     ...COMPUTED ULTIMATE SOIL RESISTANCE.
      CUSED=CAX
      PCT=2.*CAX*DX+GAMX*DX*DEPTH+2.83*CAX*DEPTH
      PCD=11.*CX*DX
      PC=DMIN1(PCT,PCD)
      IF (PCT.GT.PCD) CUSED=CX
C
C     ...COMPUTE DEFLECTION AT 50 PERCENT OF ULTIMATE SOIL RESISTANCE
      Y50=E50X*DX
C
C     ...BRANCH ACCORDING TO LOADING CRITERIA (STATIC OR CYCLIC)
      IF (KCYCL.NE.1) GO TO 140
C
C     ...STATIC CRITERIA
C
      T1=0.5*PC*DSQRT(6.*ASX)
      T2=0.411*PC
C
C     ...COMPUTE CRITICAL VALUES OF DEFLECTION
      Y2=ASX*Y50
      Y3=6.*ASX*Y50
      Y4=18.*ASX*Y50
      Y5=200.*ASX*Y50
      YINC=0.5*ASX*Y50
C
C     ...BEGIN LOOP FOR GENERATING CURVE
      DO 130 I1=1,17
C
C     ...ASSIGN DEFLECTION VALUES FOR GENERATING COMPLETE CURVE
      IF (KTHRU.EQ.0) GO TO 60
      IF (I1.EQ.1) YA=-YINC
      IF (I1.GT.13) GO TO 50
      YA=YA+YINC
      GO TO 60
   50 YINC=4.*ASX*Y50
      YA=YA+YINC
      IF (I1.EQ.17) YA=Y5
C
C     ...BEGINNING CURVED PORTION
   60 IF (YA.GT.Y2) GO TO 70
      P=0.5*PC*DSQRT(YA/Y50)
      GO TO 100
C
C     ...SECOND CURVED PORTION
   70 IF (YA.GT.Y3) GO TO 80
      T5=0.5*PC*DSQRT(YA/Y50)
      T6=0.055*PC*((YA/(ASX*Y50)-1.)**1.25)
      P=T5-T6
      GO TO 100
C
C     ...DECREASING LINEAR PORTION
   80 IF (YA.GT.Y4) GO TO 90
      T3=0.0625*PC/Y50
      T4=YA-6.*ASX*Y50
      P=T1-T2-T3*T4
      GO TO 100
C
C     ...CONSTANT LINEAR PORTION
   90 T3=0.75*PC*ASX
      P=T1-T2-T3
C
C     ...COMPUTE VALUE FROM INITIAL LINEAR PORTION
  100 PLIM=ESLIM*YA
C
C     ...ASSIGN SOIL RESISTANCE VALUE AS THE SMALLER OF THE TWO VALUES
C        FROM ES=K*X AND THAT OBTAINED FROM THE EQUATIONS IN CRITERIA
      P=DMIN1(P,PLIM)
C
C     ...COMPUTE SOIL MODULUS
      IF (YA.LT.EM14) GO TO 110
      ES(I)=P/YA
      GO TO 120
  110 ES(I)=ESLIM
C
C     ...PRINT P-Y VALUES WHERE SPECIFIED BY CODE
  120 IF (KTHRU.EQ.0) GO TO 230
      WRITE (6,260) YA,P
C
C     ...END OF LOOP FOR COMPLETE P-Y CURVE
  130 CONTINUE
      GO TO 230
C
C     ...CYCLIC CRITERIA
C
C     ...COMPUTE CRITICAL VALUES OF DEFLECTION
  140 YP=4.1*ASX*Y50
      Y2=0.45*YP
      Y3=0.6*YP
      Y4=1.8*YP
      Y5=20.*YP
      YINC=0.05*YP
C
C     ...BEGIN SOLUTION LOOP FOR COMPLETE P-Y CURVE
      DO 220 I1=1,17
C
C     ...ASSIGN VALUES OF DEFLECTION FOR GENERATING COMPLETE CURVE
      IF (KTHRU.EQ.0) GO TO 160
      IF (I1.EQ.1) YA=-YINC
      IF (I1.GT.13) GO TO 150
      YA=YA+YINC
      GO TO 160
  150 YINC=0.4*YP
      YA=YA+YINC
      IF (I1.EQ.17) YA=Y5
C
C     ...COMPUTE INITIAL PARABOLIC PORTION
  160 IF (YA.GT.Y3) GO TO 170
      P=ACX*PC*(1.-(DABS((YA-Y2)/Y2))**2.5)
      GO TO 190
C
C     ...COMPUTE DECREASING LINEAR PORTION
  170 IF (YA.GT.Y4) GO TO 180
      P=0.936*ACX*PC-0.085*PC*(YA-0.6*YP)/Y50
      GO TO 190
C
C     ...COMPUTE FINAL CONSTANT LINEAR PORTION
  180 P=0.936*ACX*PC-0.102*PC*YP/Y50
C
C     ...COMPUTE INITIAL LINEAR PORTION (ES=K*X)
  190 PLIM=ESLIM*YA
C
C     ...ASSIGN SOIL RESISTANCE VALUE THAT IS THE SMALLER OF THAT
C        COMPUTED FROM ES=K*X AND THE EQUATIONS IN THE CRITERIA
      P=DMIN1(P,PLIM)
C
C     ...COMPUTE SOIL MODULUS VALUE
      IF (YA.LT.EM14) GO TO 200
      ES(I)=P/YA
      GO TO 210
  200 ES(I)=ESLIM
C
C     ...PRINT P-Y CURVE WHERE SPECIFIED BY CODE
  210 IF (KTHRU.EQ.0) GO TO 230
      WRITE (6,260) YA,P
C
C     ...END SOLUTION LOOP FOR COMPLETE P-Y CURVE
  220 CONTINUE
C     ...END SOLUTION LOOP FOR A PARTICULAR DEPTH
  230 CONTINUE
      RETURN
C
  240 FORMAT (//9X,14HDEPTH BELOW GS,6X,4HDIAM,8X,1HC,9X,4HCAVG,6X,5HGAM
     1MA,8X,3HE50/15X,A2,13X,A2,5X,2(A3,A1,2A2,A1,2X),A3,A1,2A2,A1/9X,F1
     20.2,5X,F10.3,3(1X,D10.2),D12.4)
  250 FORMAT (/13X,4HAS =,F3.2,5X,4HAC =,F3.2,5X,2HY,,A2,13X,2HP,,A3,A1,
     1A2)
  260 FORMAT (21X,2F20.3)
C
      END
      SUBROUTINE STIFFA(ID)
C
C
C     THIS ROUTINE DOES THE FOLLOWING:
C          1)  COMPUTES VALUES OF SOIL MODULUS AT POINTS ON THE PILE
C              THAT ARE IN A LAYER SPECIFIED AS UNSUBMERGED STIFF CLAY
C          2)  GENERATES P-Y CURVES AT SPECIFIED DEPTHS USING THE
C              CRITERIA DEVELOPED BY WELCH AND REESE (1975).
C
C
      IMPLICIT REAL*8 (A-H), REAL*8 (P-Z)
      REAL*8 LENGTH,K
      CHARACTER*8 TC
      CHARACTER*4 ISYSTM
      CHARACTER*8 UN1,UN2,UN3,UN4,UN5,UN6,UN7,UN8,UN9,UN10,UN11
      COMMON / BLOC00 / TC(20)
      COMMON / BLOC04 / POTT(20),
     1                  DISTB(20),DISTA(20),THETA(20),DPS(20),
     2                  FDBET(20),KNPL,KP(20),KA(20),KS(20)
      COMMON /BLOC01/ XN(20,307),DIAM,PI,EM14,EM6,EM5,LENGTH(20),NN
      COMMON /BLOC02/ XTOP(10),XBOT(10),XGS,NL,ITOP(10),IBOT(10)
      COMMON /BLOC03/ ES(307),Y(307),LAYER(10),K(10)
      COMMON /BLOC05/ TOLP,KSOIL(10),ITOPL,IBOTL,MAXIT
      COMMON /BLOC09/ YINC,XSP(10),RCYCL,KTHRU,KCYCL,KPYOP,IT,IGP
      COMMON /BLOC13/ AE(10),FR(10),FYR,KUNIF
      COMMON /BLOC14/ UN1,UN2,UN3,UN4,UN5,UN6,UN7,UN8,UN9,UN10,UN11,
     1 ISYSTM
      IC=ID
C
C     ...BEGIN THE CALCULATION LOOP FOR OBTAINING AN ES VALUE AT EACH
C        SPECIFIED DEPTH
C
      DO 120 I=ITOPL,IBOTL
C     ...ASSIGN THE NODE COORDINATE TO A LOCAL VARIABLE FOR CONVENIENCE
C
      X=XN(IC,I)
C     ...ASSIGN DEPTH OF POINT INTO SOIL
C
      IF (KPYOP.EQ.-1) GO TO 10
      IF (KPYOP.EQ.0) XGS=DPS(IT)
      IF (KPYOP.EQ.1) XGS=DPS(IGP)
   10 DEPTH=X-XGS
C     ...ASSIGN THE FUNCTION VALUES TO LOCAL VARIABLES
C
      IF (KPYOP.NE.-1) GO TO 20
      DX=DIAM
      GO TO 30
   20 DX=D(IC,X)
   30 CX=C(X)
      CA=CAVG(X)
      GAMX=GAM(X)
      E50X=E50(X)
      IF (KTHRU.EQ.0) GO TO 40
C
C     ...PRINT HEADING AND CALCULATION PARAMETERS FOR GENERATING P-Y
C        CURVE
      WRITE (6,130) UN3,UN3,UN4,UN5,UN3,UN7,UN8,UN4,UN5,UN3,UN7,UN8,UN4,
     1UN5,UN3,UN7,UN9,DEPTH,DX,CX,CA,GAMX,E50X,UN3,UN4,UN5,UN3
C
C     ...ASSIGN THE ABSOLUTE VALUE OF THE DEFLECTION TO A LOCAL VARIABLE
   40 YA=DABS(Y(I))
C
C     ...COMPUTE ULTIMATE SOIL RESISTANCE VALUE
C
      PMT=3.*CA*DX+GAMX*DX*DEPTH+0.5*CA*DEPTH
      PMD=9.*CX*DX
      PU=DMIN1(PMT,PMD)
C
C     ...COMPUTE VALUES OF THE CRITICAL DEFLECTION ON THE P-Y CURVE
C
      Y50=2.5*E50X*DX
      YU=16.*Y50
      YINC=16./15.*Y50
      Y4=YU*1.25
      IF (KCYCL.EQ.1) GO TO 50
      YINC=Y50*(16.+9.6*DLOG10(RCYCL))/15.
      Y4=1.25*Y50*(16.+9.6*DLOG10(RCYCL))
C
C     ...ASSIGN MAXIMUM VALUE OF SOIL MODULUS (SOIL MODULUS IS
C        INFINITE AT Y=0--COMPUTE ES FOR Y=1.0E-14 AND MAKE THIS
C        THE MAXIMUM SOIL MODULUS).
C
   50 ESLIM=(PU/8.)*(EM14/Y50)**(-3./4.)
C
C     ...BEGIN THE SOLUTION LOOP FOR POINTS ON P-Y CURVE AT A SPECIFIED
C        DEPTH
      DO 110 I1=1,17
C
C     ...BRANCH PAST THE ASSIGNING OF DEFLECTIONS FOR A COMPLETE P-Y
C        CURVE WHERE ONLY AN ES VALUE IS DESIRED
      IF (KTHRU.EQ.0) GO TO 60
C
C     ...ASSIGN Y VALUES FOR GENERATING COMPLETE P-Y CURVE
      IF (I1.EQ.1) YA=-YINC
      YA=YA+YINC
      IF (I1.EQ.17) YA=Y4
C
C
   60 IF (KCYCL.EQ.0) GO TO 80
C
C     ...STATIC CRITERIA
C
      IF (YA.GT.YU) GO TO 70
      P=0.5*PU*((YA/Y50)**0.25)
      GO TO 90
C
   70 P=PU
      GO TO 90
C
C     ...CYCLIC CRITERIA
C
   80 P=0.5*PU*(YA/(Y50*(1.+0.6*DLOG10(RCYCL))))**0.25
      IF (PU.LT.P) P=PU
C
C     ...COMPUTE SOIL MODULUS VALUE
   90 ES(I)=ESLIM
      IF (YA.LT.EM14) GO TO 100
      ES(I)=P/YA
  100 CONTINUE
C
C     ...BRANCH OUT OF THE SOLUTION LOOP FOR A COMPLETE P-Y CURVE WHERE
C        ONLY AN ES VALUE IS DESIRED
      IF (KTHRU.EQ.0) GO TO 120
C
C     ...PRINT P-Y CURVE DATA
C
      WRITE (6,140) YA,P
C
C     ...END OF SOLUTION LOOP FOR A COMPLETE P-Y CURVE
  110 CONTINUE
C     ...END OF SOLUTION LOOP FOR INCREMENTING TO A NEW DEPTH
  120 CONTINUE
      RETURN
C
  130 FORMAT (//10X,14HDEPTH BELOW GS,5X,4HDIAM,8X,1HC,7X,4HCAVG,6X,5HGA
     1MMA,7X,3HE50/16X,A2,12X,A2,5X,3(A3,A1,A2,A2,A1,1X)/10X,F10.2,5X,F1
     20.3,3D10.2,D12.4/38X,1HY,19X,1HP/38X,A2,15X,A3,A1,A2)
  140 FORMAT (1X,20X,2F20.3)
C
      END
      SUBROUTINE SAND(ID,L)
C
C
C     THIS ROUTINE CALCULATES:
C       1)  SOIL MODULUS VALUES ALONG THE PILE USING THE SAND CRITERIA
C       2)  GENERATES P-Y CURVES AT SPECIFIED DEPTHS USING THE SAND
C           CRITERIA (REESE, ET AL. 1974)
C
      IMPLICIT REAL*8 (A-H), REAL*8 (P-Z)
      REAL*8 LENGTH,K,C
      CHARACTER*8 TC
      CHARACTER*4 ISYSTM
      CHARACTER*8 UN1,UN2,UN3,UN4,UN5,UN6,UN7,UN8,UN9,UN10,UN11
      COMMON / BLOC00 / TC(20)
      COMMON / BLOC04 / POTT(20),
     1                  DISTB(20),DISTA(20),THETA(20),DPS(20),
     2                  FDBET(20),KNPL,KP(20),KA(20),KS(20)
      COMMON /BLOC01/ XN(20,307),DIAM,PI,EM14,EM6,EM5,LENGTH(20),NN
      COMMON /BLOC02/ XTOP(10),XBOT(10),XGS,NL,ITOP(10),IBOT(10)
      COMMON /BLOC03/ ES(307),Y(307),LAYER(10),K(10)
      COMMON /BLOC05/ TOLP,KSOIL(10),ITOPL,IBOTL,MAXIT
      COMMON /BLOC09/ YINC,XSP(10),RCYCL,KTHRU,KCYCL,KPYOP,IT,IGP
      COMMON /BLOC14/ UN1,UN2,UN3,UN4,UN5,UN6,UN7,UN8,UN9,UN10,UN11,
     1 ISYSTM
      COMMON /BLOC19/ IC
C
      DIMENSION ASTAT(10),ACYCL(10),BSTAT(10),BCYCL(10)
C
      DATA
     1 ASTAT/2.83,2.48,2.11,1.76,1.48,1.23,1.06,0.90,0.88,0.88/,
     2 ACYCL/0.75,0.93,1.07,1.11,1.03,0.98,0.94,0.90,0.88,0.88/,
     3 BSTAT/2.14,1.83,1.54,1.27,1.05,0.86,0.71,0.53,0.50,0.50/,
     4 BCYCL/0.53,0.72,0.83,0.86,0.82,0.77,0.70,0.55,0.55,0.55/
C
C     ...STORE SOME NUMBERS.
C
      IC=ID
      DXSP=0.5
      XSP(1)=0.0
      DO 10 I=2,7
   10 XSP(I)=XSP(I-1)+DXSP
      XSP(8)=4.0
      XSP(9)=5.0
      XSP(10)=6.0
C
C     ...BEGIN SOLUTION LOOP FOR A PARTICULAR DEPTH
      DO 190 I=ITOPL,IBOTL
C
C     ...STORE FUNCTION VALUES AND ARRAY VALUES TO LOCAL VARIABLES
      X=XN(IC,I)
      IF (KPYOP.NE.-1) GO TO 20
      DX=DIAM
      GO TO 30
   20 DX=D(IC,X)
   30 GAMX=GAM(X)
      PHIX=PHI(X)
C          ...COMPUTE CONSTANTS FOR DETERMINING PC
      ALPHA=PHIX/2.
      BETA=PI/4.+ALPHA
      RK0=0.4
      RKA=DTAN(PI/4.-ALPHA)**2
C     ...COMPUTE DEPTH
      IF (KPYOP.EQ.-1) GO TO 40
      IF (KPYOP.EQ.0) XGS=DPS(IT)
      IF (KPYOP.EQ.1) XGS=DPS(IGP)
   40 DEPTH=X-XGS
C
C     ...STORE ABSOLUTE VALUE OF DEFLECTION
      YA=DABS(Y(I))
C
C     ...COMPUTE MAXIMUM VALUE OF SOIL MODULUS
      ESLIM=K(L)*DEPTH
C
C     ...COMPUTE PC NEAR GROUND SURFACE
      T1=RK0*DEPTH*DTAN(PHIX)*DSIN(BETA)/(DTAN(BETA-PHIX)*DCOS(ALPHA))
      T2=DTAN(BETA)/DTAN(BETA-PHIX)*(DX+DEPTH*DTAN(BETA)*DTAN(ALPHA))
      T3=RK0*DEPTH*DTAN(BETA)*(DTAN(PHIX)*DSIN(BETA)-DTAN(ALPHA))
      T4=RKA*DX
      PCT=GAMX*DEPTH*(T1+T2+T3-T4)
C
C     ...COMPUTE PC AT GREAT DEPTH
      T1=RKA*DX*GAMX*DEPTH*(DTAN(BETA)**8-1.)
      T2=RK0*DX*GAMX*DEPTH*DTAN(PHIX)*(DTAN(BETA)**4)
      PCD=T1+T2
C
C     ...TAKE PC AS THE MINIMUM OF THE TWO VALUES, PCT,PCD
      PC=DMIN1(PCT,PCD)
C
C     ...OBTAIN THE CORRECTION FACTORS, A AND B
      IF (KCYCL.EQ.1) GO TO 50
      ASAND=SANDAB(X,ACYCL)
      BSAND=SANDAB(X,BCYCL)
      GO TO 60
   50 ASAND=SANDAB(X,ASTAT)
      BSAND=SANDAB(X,BSTAT)
C
C     ...COMPUTE CRITICAL POINTS ON P-Y CURVE
   60 YU=3.*DX/80.
      PU=ASAND*PC
      YM=DX/60.
      PM=BSAND*PC
C
C     ...FORCE THE P-Y CURVE AT DEPTH=0 TO BE ALL ZEROS
      IF (DEPTH.GT.0.) GO TO 70
      YK=2.*DX
      GO TO 80
C
   70 SL=(PU-PM)/(YU-YM)
      RN=PM/(SL*YM)
      C = PM / ( YM ** ( 1. / RN ))
      YK=(C/ESLIM)**(RN/(RN-1.))
C
C     ...COMPUTE DEFLECTION INCREMENT TO BE USED ON CURVED PORTION OF
C        P-Y CURVE
   80 YINC=YM/12.
C
C     ...WHERE SPECIFIED BY INPUT, PRINT CALCULATION DATA FOR COMPLETE
C        P-Y CURVE
      IF (KTHRU.EQ.0) GO TO 90
      PHIDEG=PHIX*180./PI
      WRITE (6,200) UN3,UN3,UN4,UN5,UN3,UN7,UN9,DEPTH,DX,PHIDEG,GAMX,ASA
     1ND,BSAND,PCT,PCD,UN3,UN4,UN5,UN3
C
C     ...BEGIN SOLUTION LOOP FOR A COMPLETE P-Y CURVE
   90 CONTINUE
      DO 180 I1=1,17
C
      IF (KTHRU.EQ.0) GO TO 110
C
C     ...COMPUTE DEFLECTION VALUES FOR THE GENERATED P-Y CURVE
      IF (I1.EQ.1) YA=-YINC
      IF (I1.EQ.14) YA=YU-YINC
      IF (I1.GT.14) GO TO 100
      YA=YA+YINC
      GO TO 110
  100 YA=YA+DX
C
C     ...COMPUTE THE SOIL RESISTANCE VALUE FOR A PARTICULAR DEFLECTION
  110 IF (YA.GT.YK) GO TO 120
C
C     ...INITIAL LINEAR PORTION
      P=ESLIM*YA
C
      GO TO 150
C
  120 IF (YA.GT.YM) GO TO 130
C
C     ...CURVED PORTION
      P=C*(YA**(1./RN))
      GO TO 150
C
  130 IF (YA.GT.YU) GO TO 140
C
C     ...SECOND LINEAR PORTION
      P=PM+SL*(YA-YM)
      GO TO 150
C
C     ...FINAL CONSTANT PORTION
  140 P=PU
C
C     ...COMPUTE MAXIMUM VALUE OF P
  150 PLIM=ESLIM*YA
C
C     ...DETERMINE THAT P FROM CRITERIA IS LESS THAN THE MAXIMUM VALUE
      P=DMIN1(P,PLIM)
C
C     ...COMPUTE SOIL MODULUS VALUE
      IF (YA.LT.EM14) GO TO 160
      ES(I)=P/YA
      GO TO 170
  160 ES(I)=ESLIM
C
C     ...TRANSFER OUT OF LOOP, WHERE ONLY AN ES VALUE IS WANTED
  170 IF (KTHRU.EQ.0) GO TO 190
C
C     ...PRINT THE VALUES FOR THE GENERATED P-Y CURVE
      WRITE (6,210) YA,P
C
C     ...END OF SOLUTION LOOP FOR COMPLETE P-Y CURVE
  180 CONTINUE
C
C     ...END OF SOLUTION LOOP FOR A PARTICULAR DEPTH
  190 CONTINUE
      RETURN
C
  200 FORMAT (//11X,14HDEPTH BELOW GS,6X,4HDIAM,6X,3HPHI,5X,5HGAMMA,10X,
     11HA,9X,1HB,15X,3HPCT,7X,3HPCD/17X,A2,13X,A2,13X,A3,A1,A2,A2,A1/10X
     2,F10.2,5X,2F10.2,D10.2,2F10.2,10X,2D10.2//38X,1HY,19X,1HP/38X,A2,1
     35X,A3,A1,A2)
  210 FORMAT (21X,2F20.3)
C
      END
      SUBROUTINE CLAY(ID,L)
C
C
C     THIS ROUTINE DOES THE FOLLOWING:
C          1)  COMPUTES VALUES OF SOIL MODULUS AT POINTS ON THE PILE
C              THAT ARE IN A LAYER SPECIFIED AS CLAY
C          2)  GENERATES P-Y CURVES AT SPECIFIED DEPTHS USING THE
C              UNIFIED CLAY CRITERIA (SULLIVAN, 1977)
C
C
      IMPLICIT REAL*8 (A-H), REAL*8 (P-Z)
      REAL*8 LENGTH,K
      CHARACTER*8 TC
      CHARACTER*4 ISYSTM
      CHARACTER*8 UN1,UN2,UN3,UN4,UN5,UN6,UN7,UN8,UN9,UN10,UN11
      COMMON / BLOC00 / TC(20)
      COMMON / BLOC04 / POTT(20),
     1                  DISTB(20),DISTA(20),THETA(20),DPS(20),
     2                  FDBET(20),KNPL,KP(20),KA(20),KS(20)
      COMMON /BLOC01/ XN(20,307),DIAM,PI,EM14,EM6,EM5,LENGTH(20),NN
      COMMON /BLOC02/ XTOP(10),XBOT(10),XGS,NL,ITOP(10),IBOT(10)
      COMMON /BLOC03/ ES(307),Y(307),LAYER(10),K(10)
      COMMON /BLOC05/ TOLP,KSOIL(10),ITOPL,IBOTL,MAXIT
      COMMON /BLOC09/ YINC,XSP(10),RCYCL,KTHRU,KCYCL,KPYOP,IT,IGP
      COMMON /BLOC13/ AE(10),FR(10),FYR,KUNIF
      COMMON /BLOC14/ UN1,UN2,UN3,UN4,UN5,UN6,UN7,UN8,UN9,UN10,UN11,
     1 ISYSTM
C
      LL=L
      IC=ID
C
C     ...BEGIN THE CALCULATION LOOP FOR OBTAINING AN ES VALUE AT EACH
C        SPECIFIED DEPTH
C
      DO 170 I=ITOPL,IBOTL
C     ...ASSIGN THE NODE COORDINATE TO A LOCAL VARIABLE FOR CONVENIENCE
C
      X=XN(IC,I)
C     ...ASSIGN DEPTH OF POINT INTO SOIL
C
      IF (KPYOP.EQ.-1) GO TO 10
      IF (KPYOP.EQ.0) XGS=DPS(IT)
      IF (KPYOP.EQ.1) XGS=DPS(IGP)
   10 DEPTH=X-XGS
C     ...ASSIGN THE FUNCTION VALUES TO LOCAL VARIABLES
C
      IF (KPYOP.NE.-1) GO TO 20
      DX=DIAM
      GO TO 30
   20 DX=D(IC,X)
   30 CX=C(X)
      CA=CAVG(X)
      GAMX=GAM(X)
      E50X=E50(X)
      IF (KTHRU.EQ.0) GO TO 40
C
C     ...PRINT HEADING AND CALCULATION PARAMETERS FOR GENERATING P-Y
C        CURVE
      WRITE (6,180) UN3,UN3,UN4,UN5,UN3,UN7,UN8,UN4,UN5,UN3,UN7,UN8,UN4,
     1UN5,UN3,UN7,UN9,DEPTH,DX,CX,CA,GAMX,E50X,UN3,UN4,UN5,UN3
C
C     ...ASSIGN THE ABSOLUTE VALUE OF THE DEFLECTION TO A LOCAL VARIABLE
   40 YA=DABS(Y(I))
C
C     ...ASSIGN LIMITING MAXIMUM VALUE OF SOIL MODULUS
      ESLIM=K(L)*DEPTH
      IF (DEPTH.LT.EM6) ESLIM=K(L)
C
C     ...COMPUTE ULTIMATE SOIL RESISTANCE VALUE
C
      XR=12.*DX
      IF (DEPTH.GT.XR) GO TO 50
      PM1=2.*CA*DX+GAMX*DEPTH*DX+.833*DEPTH*CA
      PM2=(3.+.5*DEPTH/DX)*CX*DX
      PM=DMIN1(PM1,PM2)
      GO TO 60
   50 PM=9.*CX*DX
C
C     ...COMPUTE VALUES OF THE CRITICAL DEFLECTION ON THE P-Y CURVE
C
   60 Y50=AE(LL)*E50X*DX
      Y0=0.5*Y50
      IF (DEPTH.GT.0.) Y0=((0.5*PM/ESLIM)**1.5)*(Y50**(-0.5))
      Y1=Y50
      IF (KCYCL.EQ.1) Y1=8.*Y50
      YR=FYR*Y50
      Y4=(FYR+10.)*Y50
C
C     ...COMPUTE DEFLECTION INCREMENT FOR CURVED PORTION
      YINC=Y1/12.
C
C     ...COMPUTE CRITICAL VALUES OF SOIL RESISTANCE ON P-Y CURVE
      P1=0.5*PM*((Y1/Y50)**(1./3.))
      PR=(FR(LL)+(1.-FR(LL))*DEPTH/XR)*PM
      IF (DEPTH.GT.XR) PR=PM
      PRCYC=DEPTH*P1/XR
      IF (DEPTH.GT.XR) PRCYC=P1
C
C     ...BEGIN THE SOLUTION LOOP FOR POINTS ON P-Y CURVE AT A SPECIFIED
C        DEPTH
      DO 160 I1=1,17
C
C     ...BRANCH PAST THE ASSIGNING OF DEFLECTIONS FOR A COMPLETE P-Y
C        CURVE WHERE ONLY AN ES VALUE IS DESIRED
      IF (KTHRU.EQ.0) GO TO 80
C
C     ...ASSIGN Y VALUES FOR GENERATING COMPLETE P-Y CURVE
      IF (I1.GT.13) GO TO 70
      IF (I1.EQ.1) YA=-YINC
      YA=YA+YINC
      GO TO 80
   70 IF (I1.EQ.14) YA=Y1+(YR-Y1)/3.
      IF (I1.EQ.15) YA=Y1+(YR-Y1)*2./3.
      IF (I1.EQ.16) YA=YR
      IF (I1.EQ.17) YA=Y4
C
C     ...COMPUTE THE VALUE OF SOIL RESISTANCE FOR A PARTICULAR VALUE
C        OF DEFLECTION
C
   80 IF (YA.GT.Y1) GO TO 90
C
C     ...CURVED PORTION
      P=0.5*PM*((YA/Y50)**(1./3.))
      GO TO 130
C
C     ...BRANCH FOR CYCLIC OR STATIC LOADING
   90 IF (KCYCL.EQ.1) GO TO 110
C
      IF (YA.GT.YR) GO TO 100
C
C     ...DECREASING LINEAR PORTION FOR DEPTHS ABOVE CRITICAL
      S1=(PRCYC-P1)/(YR-Y1)
      P=P1+S1*(YA-Y1)
      GO TO 130
C
C     ...CONSTANT RESISTANCE AT RESIDUAL VALUE
  100 P=PRCYC
      GO TO 130
C
C     ...STATIC RESISTANCE
C
  110 IF (YA.GT.YR) GO TO 120
C
C     ...DECREASING LINEAR PORTION
      P=PM+(PR-PM)/(YR-Y1)*(YA-Y1)
      GO TO 130
C
C     ...CONSTANT RESISTANCE AT THE RESIDUAL VALUE
  120 P=PR
C
C     ...COMPUTE SOIL MODULUS VALUE
  130 PLIM=ESLIM*YA
      P=DMIN1(P,PLIM)
      IF (YA.LT.EM14) GO TO 140
      ES(I)=P/YA
      GO TO 150
  140 ES(I)=ESLIM
C
C     ...BRANCH OUT OF THE SOLUTION LOOP FOR A COMPLETE P-Y CURVE WHERE
C        ONLY AN ES VALUE IS DESIRED
  150 IF (KTHRU.EQ.0) GO TO 170
C
C     ...PRINT P-Y CURVE DATA
C
      WRITE (6,190) YA,P
      IF (I1.EQ.1.AND.Y0.GT.Y50) WRITE (6,200) Y0,UN1
C
C     ...END OF SOLUTION LOOP FOR A COMPLETE P-Y CURVE
  160 CONTINUE
C     ...END OF SOLUTION LOOP FOR INCREMENTING TO A NEW DEPTH
  170 CONTINUE
      RETURN
C
  180 FORMAT (//10X,14HDEPTH BELOW GS,5X,4HDIAM,8X,1HC,7X,4HCAVG,6X,5HGA
     1MMA,7X,3HE50/16X,A2,12X,A2,5X,3(A3,A1,A2,A2,A1,1X)/10X,F10.2,5X,F1
     20.3,3D10.2,D12.4/38X,1HY,19X,1HP/38X,A2,15X,A3,A1,A2)
  190 FORMAT (21X,2F20.3)
  200 FORMAT (/29X,17H**** WARNING ****/5X,57HTHE SOIL RESPONSE IS DEFIN
     1ED BY ES=K*X TO A DEFLECTION OF,1X,D10.3,A3,1H.)
C
      END
      FUNCTION D(ITYPE,XL)
C
C
C     THIS ROUTINE ASSIGNS A VALUE OF PILE DIAMETER AS A FUNCTION OF X
C
C
      IMPLICIT REAL*8 (A-H),REAL*8 (P-Z)
      REAL*8 LENGTH
C
      COMMON /BLOC01/ XN(20,307),DIAM,PI,EM14,EM6,EM5,LENGTH(20),NN
      COMMON / BLOC07 / XINC(20), YIELD(20), YOUNG(20),
     1                  NDS(20), NI(20), KTYPE(20)
      COMMON / BLOC10 / XX1(20,5), XX2(20,5), RRI(20,5),
     1                  SIZE(20,5), AREA(20,5), XRI(20,5),
     2                  BULT(20,5,20), PULT(20,5,20), NINTER(20,5)
C
      IC=ITYPE
      J=NDS(IC)+1
      X=XL
C
      XX1(IC,J)=LENGTH(IC)*1.2
      JK=NDS(IC)
      DO 10 JD=1,JK
      IF (X.LT.XX1(IC,JD+1)) GO TO 20
   10 CONTINUE
   20 D=SIZE(IC,JD)
      RETURN
C
      END
      FUNCTION XRII(ITYPE,XL)
C
C
C     THIS ROUTINE ASSIGNS A VALUE OF PILE MONENT OF INERTIA AS A
C     FUNCTION OF DEPTH X
C
C
      IMPLICIT REAL*8 (A-H),REAL*8 (P-Z)
      REAL*8 LENGTH
C
      COMMON /BLOC01/ XN(20,307),DIAM,PI,EM14,EM6,EM5,LENGTH(20),NN
      COMMON / BLOC07 / XINC(20), YIELD(20), YOUNG(20),
     1                  NDS(20), NI(20), KTYPE(20)
      COMMON / BLOC10 / XX1(20,5), XX2(20,5), RRI(20,5),
     1                  SIZE(20,5), AREA(20,5), XRI(20,5),
     2                  BULT(20,5,20), PULT(20,5,20), NINTER(20,5)
C
      IC=ITYPE
      X=XL
      J=NDS(IC)+1
      JK=NDS(IC)
C
      XX1(IC,J)=LENGTH(IC)*1.2
      DO 10 JD=1,JK
      IF (X.LT.XX1(IC,JD+1)) GO TO 20
   10 CONTINUE
   20 XRII=XRI(IC,JD)
      RETURN
C
      END
      FUNCTION AREA1(ITYPE,XL)
C
C
C     THIS ROUTINE ASSIGNS A VALUE OF PILE CROSS-SECTION AREA AS A
C     FUNCTION OF DEPTH X
C
C
      IMPLICIT REAL*8 (A-H),REAL*8 (P-Z)
      REAL*8 LENGTH
      COMMON /BLOC01/ XN(20,307),DIAM,PI,EM14,EM6,EM5,LENGTH(20),NN
      COMMON / BLOC07 / XINC(20), YIELD(20), YOUNG(20),
     1                  NDS(20), NI(20), KTYPE(20)
      COMMON / BLOC10 / XX1(20,5), XX2(20,5), RRI(20,5),
     1                  SIZE(20,5), AREA(20,5), XRI(20,5),
     2                  BULT(20,5,20), PULT(20,5,20), NINTER(20,5)
C
C
      IC=ITYPE
      X=XL
      J=NDS(IC)+1
      JK=NDS(IC)
C
      XX1(IC,J)=LENGTH(IC)*1.2
      DO 10 JD=1,JK
      IF (X.LT.XX1(IC,JD+1)) GO TO 20
   10 CONTINUE
   20 AREA1=AREA(IC,JD)
      RETURN
C
      END
      FUNCTION GAM(XL)
C
C
C     THIS ROUTINE ASSIGNS A VALUE OF AVERAGE EFFECTIVE UNIT WEIGHT
C     TO POINTS ON THE PILE AS A FUNCTION OF DEPTH.  THE VALUE IS
C     OBTAINED BY AVERAGING THE VALUES OF EFFECTIVE UNIT WEIGHT ABOVE
C     THE POINT OF INTEREST.  VALUES OF EFFECTIVE UNIT WEIGHT ARE
C     OBTAINED FROM AN INPUT CURVE OF EFFECTIVE UNIT WEIGHT VS. DEPTH
C     BY INTERPOLATING LINEARLY BETWEEN POINTS ON THE INPUT CURVE.
C
C
      IMPLICIT REAL*8 (A-H),REAL*8 (P-Z)
      REAL*8 LENGTH
      COMMON /BLOC01/ XN(20,307),DIAM,PI,EM14,EM6,EM5,LENGTH(20),NN
      COMMON /BLOC02/ XTOP(10),XBOT(10),XGS,NL,ITOP(10),IBOT(10)
      COMMON /BLOC08/ XG1(10),GAM1(10)
     1 ,XSTR(10),C1(10),PHI1(10),EE50(10),NSTR,NG1
      X=XL
C
C
      DEPTH=X-XGS
      GAM=0.
      IF (X.LT.XGS) GO TO 60
      DO 10 J=2,NG1
      IF (DEPTH.LT.EM6) GO TO 30
      IF (X.LT.XG1(J)) GO TO 20
C
C     ...COMPUTE THE AVERAGE EFF. UNIT WEIGHT BETWEEN TWO INPUT POINTS
C        AND WEIGHT IT USING THE DISTANCE BETWEEN THE POINTS
C
      WGAM=(GAM1(J)+GAM1(J-1))*0.5*((XG1(J)-XG1(J-1))/DEPTH)
      GAM=GAM+WGAM
   10 CONTINUE
      J=NG1
      IF (X.LE.XG1(NG1)) GO TO 60
      WRITE (6,70) X
      STOP 4
C
C     ...COMPUTE AN AVERAGE VALUE FOR POINTS LYING BETWEEN INPUT POINTS
   20 DELX=XG1(J)-XG1(J-1)
      IF (DELX.LT.EM6) GO TO 60
      SL=(GAM1(J)-GAM1(J-1))/DELX
      GAMX=GAM1(J-1)+SL*(X-XG1(J-1))
      WGAMF=(GAMX+GAM1(J-1))*0.5*((X-XG1(J-1))/DEPTH)
C
C     ...SUM ALL THE WEIGHTED AVERAGE VALUES
      GAM=GAM+WGAMF
      GO TO 60
   30 DO 40 J=1,NG1
      IF (GAM1(J).GT.0.) GO TO 50
   40 CONTINUE
      J=NG1
   50 GAM=GAM1(J)
   60 CONTINUE
      RETURN
C
   70 FORMAT (/29X,17H**** WARNING ****/5X,16HTHE VALUE OF X =,E9.2,24H
     1IS BEYOND THE EXTENT OF/5X,31HTHE INPUT CURVE FOR UNIT WEIGHT)
C
      END
      FUNCTION E50(XL)
C
C
C     THIS ROUTINE ASSIGNS A VALUE OF STRAIN AT 50 PERCENT OF ULTIMATE
C     TO POINTS ON THE PILE AS A FUNCTION OF DEPTH.  THE VALUES ARE
C     OBTAINED EITHER FROM AN INPUT CURVE OF E50 VS. DEPTH BY INTER-
C     POLATING LINEARLY BETWEEN POINTS ON THE INPUT CURVE OR BY ASSIGN-
C     ING DEFAULT VALUES BASED ON THE SHEAR STRENGTH OF THE SOIL.
C
C
      IMPLICIT REAL*8 (A-H),REAL*8 (P-Z)
      REAL*8 LENGTH
      COMMON /BLOC01/ XN(20,307),DIAM,PI,EM14,EM6,EM5,LENGTH(20),NN
      COMMON /BLOC08/ XG1(10),GAM1(10)
     1 ,XSTR(10),C1(10),PHI1(10),EE50(10),NSTR,NG1
      X=XL
C
C     ...OBTAIN VALUES BY INTERPOLATING ON INPUT CURVE
C
      DO 10 J=2,NSTR
      IF (X.LT.XSTR(J)) GO TO 20
   10 CONTINUE
      J=NSTR
   20 DELX=XSTR(J)-XSTR(J-1)
      IF (DELX.LT.EM6) GO TO 30
      SL=(EE50(J)-EE50(J-1))/DELX
      E50=EE50(J-1)+SL*(X-XSTR(J-1))
      GO TO 40
   30 E50=(EE50(J-1)+EE50(J))/2.
   40 CONTINUE
      RETURN
C
      END
      FUNCTION C(XL)
C
C
C     THIS ROUTINE ASSIGNS A VALUE OF UNDRAINED SHEAR STRENTH TO POINTS
C     ON THE PILE AS A FUNCTION OF DEPTH.  THE VALUES ARE OBTAINED FROM
C     AN INPUT CURVE OF SHEAR STRENGTH VS. DEPTH BY INTERPOLATING
C     LINEARLY BETWEEN POINTS ON THE INPUT CURVE.
C
C
      IMPLICIT REAL*8 (A-H),REAL*8 (P-Z)
      REAL*8 LENGTH
      COMMON /BLOC01/ XN(20,307),DIAM,PI,EM14,EM6,EM5,LENGTH(20),NN
      COMMON /BLOC08/ XG1(10),GAM1(10)
     1 ,XSTR(10),C1(10),PHI1(10),EE50(10),NSTR,NG1
      X=XL
C
C
      DO 10 J=2,NSTR
      IF (X.LT.XSTR(J)) GO TO 20
   10 CONTINUE
      J=NSTR
      IF (X.LE.XSTR(NSTR)) GO TO 20
      WRITE (6,50) X
      STOP 5
   20 DELX=XSTR(J)-XSTR(J-1)
      IF (DELX.LT.EM6) GO TO 30
      SL=(C1(J)-C1(J-1))/(DELX)
      C=C1(J-1)+SL*(X-XSTR(J-1))
      GO TO 40
   30 C=(C1(J-1)+C1(J))/2.
   40 CONTINUE
      RETURN
C
   50 FORMAT (/29X,17H**** WARNING ****/5X,16HTHE VALUE OF X =,D9.2,24H
     1IS BEYOND THE EXTENT OF/5X,24HTHE INPUT STRENGTH CURVE)
C
      END
      FUNCTION CAVG(XL)
C
C
C     THIS ROUTINE COMPUTES AN AVERAGE VALUE FOR SHEAR STRENGTH FROM
C     GROUND SURFACE TO ANY DEPTH USING AN INPUT CURVE OF SHEAR
C     STRENGTH VS. X.
C
C
      IMPLICIT REAL*8 (A-H),REAL*8 (P-Z)
      REAL*8 LENGTH
      COMMON /BLOC01/ XN(20,307),DIAM,PI,EM14,EM6,EM5,LENGTH(20),NN
      COMMON /BLOC02/ XTOP(10),XBOT(10),XGS,NL,ITOP(10),IBOT(10)
      COMMON /BLOC08/ XG1(10),GAM1(10)
     1 ,XSTR(10),C1(10),PHI1(10),EE50(10),NSTR,NG1
      X=XL
C
      DEPTH=X-XGS
      CAVG=0.
      IF (X.LT.XGS) GO TO 40
      DO 10 J=2,NSTR
      IF (DEPTH.LT.EM6) GO TO 30
      IF (X.LT.XSTR(J)) GO TO 20
      WC=(C1(J)+C1(J-1))*0.5*(XSTR(J)-XSTR(J-1))/DEPTH
      CAVG=CAVG+WC
   10 CONTINUE
      J=NSTR
      IF (X.LE.XSTR(NSTR)) GO TO 40
      WRITE (6,50) X
   20 DELX=XSTR(J)-XSTR(J-1)
      IF (DELX.LT.EM6) GO TO 40
      SL=(C1(J)-C1(J-1))/DELX
      CX=C1(J-1)+SL*(X-XSTR(J-1))
      WCF=(CX+C1(J-1))*0.5*(X-XSTR(J-1))/DEPTH
      CAVG=CAVG+WCF
      GO TO 40
   30 CAVG=C(X)
   40 RETURN
C
   50 FORMAT (/29X,17H**** WARNING ****/5X,16HTHE VALUE OF X =,E9.2,28H
     1IS BEYOND THE EXTENT OF THE/5X,24HINPUT CURVE FOR STRENGTH)
C
      END
      FUNCTION AS(XL)
C
C
C     THIS ROUTINE INTERPOLATES FOR VALUES OF COEFFICIENT AS FOR STIFF
C     CLAY BELOW WATER TABLE CRITERIA
C
C
      IMPLICIT REAL*8 (A-H),REAL*8 (P-Z)
      REAL*8 LENGTH
      COMMON /BLOC01/ XN(20,307),DIAM,PI,EM14,EM6,EM5,LENGTH(20),NN
      COMMON /BLOC02/ XTOP(10),XBOT(10),XGS,NL,ITOP(10),IBOT(10)
      COMMON /BLOC09/ YINC,XSP(10),RCYCL,KTHRU,KCYCL,KPYOP,IT,IGP
      COMMON /BLOC19/ IC
C
      DIMENSION XPD(6),AS1(6)
C
      DATA  XPD/0.,1.,2.,3.,4.,5./ ,AS1/0.223,0.46,0.55,0.59,0.6,0.6/
      X=XL
C
      IF (KPYOP.NE.-1) GO TO 10
      DX=DIAM
      GO TO 20
   10 DX=D(IC,X)
   20 XA=(X-XGS)/DX
      DO 30 J=2,6
      IF (XA.LT.XPD(J)) GO TO 40
   30 CONTINUE
      J=6
   40 DXPD=XPD(J)-XPD(J-1)
      SL=(AS1(J)-AS1(J-1))/DXPD
      AS=AS1(J-1)+SL*(XA-XPD(J-1))
      RETURN
C
      END
      FUNCTION AC(XL)
C
C
C     THIS ROUTINE INTERPOLATES ON A CURVE OF THE COEFF. AC VERSUS
C     DIMENSIONLESS DEPTH.
C
C
      IMPLICIT REAL*8 (A-H),REAL*8 (P-Z)
      REAL*8 LENGTH
      COMMON /BLOC01/ XN(20,307),DIAM,PI,EM14,EM6,EM5,LENGTH(20),NN
      COMMON /BLOC02/ XTOP(10),XBOT(10),XGS,NL,ITOP(10),IBOT(10)
      COMMON /BLOC09/ YINC,XSP(10),RCYCL,KTHRU,KCYCL,KPYOP,IT,IGP
      COMMON /BLOC19/ IC
C
C
      DIMENSION XPD(4),AC1(4)
C
      DATA XPD/0.,1.,2.,3./ ,AC1/0.2,0.29,0.3,0.3/
      X=XL
C
      IF (KPYOP.NE.-1) GO TO 10
      DX=DIAM
      GO TO 20
   10 DX=D(IC,X)
   20 XA=(X-XGS)/DX
      DO 30 J=2,4
      IF (XA.LT.XPD(J)) GO TO 40
   30 CONTINUE
      J=4
   40 DXPD=XPD(J)-XPD(J-1)
      SL=(AC1(J)-AC1(J-1))/DXPD
      AC=AC1(J-1)+SL*(XA-XPD(J-1))
      RETURN
C
      END
      FUNCTION PHI(XL)
C
C     THIS ROUTINE COMPUTES VALUES OF ANGLE OF INTERNAL FRICTION AS A
C     FUNCTION OF DEPTH FROM AN INPUT CURVE.
C
      IMPLICIT REAL*8 (A-H),REAL*8 (P-Z)
      REAL*8 LENGTH
      COMMON /BLOC01/ XN(20,307),DIAM,PI,EM14,EM6,EM5,LENGTH(20),NN
      COMMON /BLOC08/ XG1(10),GAM1(10)
     1 ,XSTR(10),C1(10),PHI1(10),EE50(10),NSTR,NG1
      X=XL
C
C
      DO 10 J=2,NSTR
      IF (X.LT.XSTR(J)) GO TO 20
   10 CONTINUE
      J=NSTR
      IF (X.LE.XSTR(NSTR)) GO TO 20
      WRITE (6,50) X
      STOP 6
   20 DELX=XSTR(J)-XSTR(J-1)
      IF (DELX.LT.EM6) GO TO 30
      SL=(PHI1(J)-PHI1(J-1))/DELX
      PHI=PHI1(J-1)+SL*(X-XSTR(J-1))
      GO TO 40
   30 PHI=(PHI1(J)+PHI1(J-1))/2.
   40 CONTINUE
      RETURN
C
   50 FORMAT (/29X,17H**** WARNING ****/5X,16HTHE VALUE OF X =,E9.2,24H
     1IS BEYOND THE EXTENT OF/5X,24HTHE INPUT STRENGTH CURVE)
C
      END
      FUNCTION SANDAB(X,FAB)
C
C
C     THIS ROUTINE COMPUTES VALUES OF A AND B FOR THE SAND CRITERIA FOR
C     BOTH STATIC AND CYCLIC LOADING.
C
C
      IMPLICIT REAL*8 (A-H),REAL*8 (P-Z)
      REAL*8 LENGTH
      COMMON /BLOC01/ XN(20,307),DIAM,PI,EM14,EM6,EM5,LENGTH(20),NN
      COMMON /BLOC02/ XTOP(10),XBOT(10),XGS,NL,ITOP(10),IBOT(10)
      COMMON /BLOC09/ YINC,XSP(10),RCYCL,KTHRU,KCYCL,KPYOP,IT,IGP
      COMMON /BLOC19/ IC
C
      DIMENSION FAB(10)
C
      IF (KPYOP.NE.-1) GO TO 10
      DX=DIAM
      GO TO 20
   10 DX=D(IC,X)
   20 XA=(X-XGS)/DX
      DO 30 J=2,10
      IF (XA.LT.XSP(J)) GO TO 40
   30 CONTINUE
      J=10
   40 DXSP=XSP(J)-XSP(J-1)
      SLA=(FAB(J)-FAB(J-1))/DXSP
      SANDAB=FAB(J-1)+SLA*(XA-XSP(J-1))
      RETURN
C
      END

